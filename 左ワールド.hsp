randomize
screen 0,480,360
;ハイスコア初期化
HISC=0
title "左ワールド"
#include "mod_menu.as"

;メニューアイテムIDを定義
#enum CMD_MESS; [表示]アイテムのID

;メッセージ割り込み
oncmd  gosub *OnCommand, WM_COMMAND
onexit gosub *OnExitMess


;メニューバーの作成
newmenu hmenu,0
addmenu hmenu, "ヘルプ(&H)", CMD_MESS
        
;メニュー割当
applymenu hmenu

dialog "開始します。よろしいですか？",2,"左ワールド"
	if stat=6 :goto *GAMESTART
	end

;メッセージの処理
*OnCommand
	switch (wparam & 0xFFFF)
        case CMD_MESS   ;ヘルプ
        	dialog "左クリック押しつづけで上昇 はなすと下降 壁にぶつかるとゲームオーバー", 0, "左ワールドのヘルプ"
        	swbreak
        swend
        return



*OnExitMess
	dialog "終了します。よろしいですか？",2,"左ワールド"
	if stat=6 :end
        

*GAMESTART
	;画面を黒で塗りつぶし
	cls 4
	UEKABE=100
	SUKIMA=200
	MY=180
	SPEED=0.0
	;スコア初期化
	SC=0

*DRAWLOOP
	;スコアからレベルを算出
	LV=SC/500+1
	;500点ごとに難易度上昇
	if LV>18 :LV=18
	
	;右端に黒線を引く
	color 0,0,0
	line 479,0,479,359
	;上壁を引く
	color 150,LV*40,0
	R=rnd(LV*2+1)-LV
	UEKABE=UEKABE+R
	;上壁の高さ制限
	if UEKABE<10 :UEKABE=10
	if UEKABE>250 :UEKABE=250
	line 479,0,479,UEKABE

	;隙間の高さを決める
	R=rnd(LV*2+1)-LV
	SUKIMA=SUKIMA+R
	;隙間の高さ制限
	if SUKIMA<100 :SUKIMA=100
	if SUKIMA>200 :SUKIMA=200
	;下壁を引く
	line 479,UEKABE+SUKIMA,479,359

	;当たり判定。rvalが0以外ならゲームオーバー
	pget 340,MY
	if ginfo_r!0 :goto *GAMEOVER
	;白点の表示
	color 255,255,255
	pset 340,MY
	
	;スペースキーをチェック
	stick KY,256
	if KY=256 :SPEED=SPEED-0.25 :else : SPEED=SPEED+0.25
	if SPEED>3.0 :SPEED=3.0
	if SPEED<-3.0 :SPEED=-3.0
	MY=MY+int(SPEED)
	
	;コピーする
	pos 0,0
	gcopy 0,1,0,479,360
	
	;スコア表示
	SC=SC+1
	color 0,0,0
	boxf 80,0,270,16
	pos 81,0
	color 255,255,255
	mes "ﾚﾍﾞﾙ" + LV + "/" + "ﾊｲｽｺｱ " + HISC + "/" + SC 
	
	wait 5
	goto *DRAWLOOP

*GAMEOVER
	;ハイスコア設定
	if HISC<SC :HISC=SC
	wait 100
	dialog "再チャレンジしますか？",2,"左ワールド"
	if stat=6 :goto *GAMESTART
	end
